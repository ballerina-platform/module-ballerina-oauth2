/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - OAuth2 Ballerina'

def packageName = "oauth2"
def packageOrg = "ballerina"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
}

task updateTomlFiles {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

task startWso2IS() {
    doLast {
        // This check is added to prevent starting the server in Windows OS, since the Docker image does not support
        // for Windows OS.
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=wso2-is"
                standardOutput = stdOut
            }
            if (!stdOut.toString().contains("wso2-is")) {
                println "Starting WSO2 IS."
                exec {
                    commandLine 'sh', '-c', "docker run --rm -d -p 9443:9443 --name wso2-is ldclakmal/wso2is-sts:latest"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                println "Waiting 100s until the WSO2 IS get initiated."
                sleep(100 * 1000)
            } else {
                println "WSO2 IS is already started."
            }
        }
    }
}

task stopWso2IS() {
    doLast {
        // This check is added to prevent trying to stop the server in Windows OS, since the Docker image not started
        // in Windows OS.
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=wso2-is"
                standardOutput = stdOut
            }
            if (stdOut.toString().contains("wso2-is")) {
                println "Stopping WSO2 IS."
                exec {
                    commandLine 'sh', '-c', "docker stop wso2-is"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                println "Waiting 15s until the WSO2 IS get stopped."
                sleep(15 * 1000)
            } else {
                println "WSO2 IS is not started."
            }
        }
    }
}

task startBallerinaSTS() {
    doLast {
        // This check is added to prevent starting the server in Windows OS, since the Docker image does not support
        // for Windows OS.
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=ballerina-sts"
                standardOutput = stdOut
            }
            if (!stdOut.toString().contains("ballerina-sts")) {
                println "Starting Ballerina STS."
                exec {
                    commandLine 'sh', '-c', "docker run --rm -d -p 9445:9445 -p 9444:9444 --name ballerina-sts ldclakmal/ballerina-sts:latest"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                println "Waiting 10s until the Ballerina STS get initiated."
                sleep(15 * 1000)
            } else {
                println "Ballerina STS is already started."
            }
        }
    }
}

task stopBallerinaSTS() {
    doLast {
        // This check is added to prevent trying to stop the server in Windows OS, since the Docker image not started
        // in Windows OS.
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=ballerina-sts"
                standardOutput = stdOut
            }
            if (stdOut.toString().contains("ballerina-sts")) {
                println "Stopping Ballerina STS."
                exec {
                    commandLine 'sh', '-c', "docker stop ballerina-sts"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                println "Waiting 15s until the Ballerina STS get stopped."
                sleep(15 * 1000)
            } else {
                println "Ballerina STS is not started."
            }
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs

test.finalizedBy stopWso2IS
test.finalizedBy stopBallerinaSTS
test.dependsOn startWso2IS
test.dependsOn startBallerinaSTS
test.dependsOn ":${packageName}-native:build"

build.dependsOn "generatePomFileForMavenPublication"
build.finalizedBy stopWso2IS
build.finalizedBy stopBallerinaSTS
build.dependsOn startWso2IS
build.dependsOn startBallerinaSTS
build.dependsOn ":${packageName}-native:build"

publishToMavenLocal.dependsOn build
publish.dependsOn build
